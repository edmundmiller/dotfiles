{
  config,
  options,
  pkgs,
  lib,
  ...
}:
with lib;
with lib.my;
let
  cfg = config.modules.shell.tmux;
  inherit (config.dotfiles) configDir;

  # Fetch tmux-opencode-status plugin for OpenCode/Claude agent activity indicators
  # Shows states: idle (○), busy (●), waiting (◉), error (✗), finished (✔)
  tmux-opencode-status = pkgs.fetchFromGitHub {
    owner = "IFAKA";
    repo = "tmux-opencode-status";
    rev = "d1cfa0e7663b0c9c4e6a51a1585986096f46ce8c";
    sha256 = "sha256-ZoXNJPDsggi5d+5jcPAOUdTTOecIsfZrfSmE4nZSkNY=";
  };

  # Despite tmux/tmux#142, tmux will support XDG in 3.2. Sadly, only 3.0 is
  # available on nixpkgs, and 3.1b on master (tmux/tmux@15d7e56), so I
  # implement it myself:
  # Export environment variables with fallback defaults for when they aren't
  # set (e.g., ghostty launching with --noprofile --norc). These are needed
  # by the tmux config file itself for sourcing extraInit, swap-pane scripts,
  # and the reload binding.
  tmux = pkgs.writeScriptBin "tmux" ''
    #!${pkgs.stdenv.shell}
    export TMUX_HOME="''${TMUX_HOME:-$HOME/.config/tmux}"
    export DOTFILES="''${DOTFILES:-$HOME/.config/dotfiles}"
    export DOTFILES_BIN="''${DOTFILES_BIN:-$DOTFILES/bin}"
    exec ${pkgs.tmux}/bin/tmux -f "$TMUX_HOME/config" "$@"
  '';
in
{
  options.modules.shell.tmux = with types; {
    enable = mkBoolOpt false;
    rcFiles = mkOpt (listOf (either str path)) [ "${configDir}/tmux/theme.conf" ];
  };

  config = mkIf cfg.enable {
    user.packages = [
      tmux
      pkgs.gum # Interactive CLI for bd-capture popup
    ];

    modules.theme.onReload.tmux = "${tmux}/bin/tmux source-file $TMUX_HOME/extraInit";

    modules.shell.zsh = {
      rcInit = "_cache tmuxifier init -";
      rcFiles = [ "${configDir}/tmux/aliases.zsh" ];
    };

    home.configFile = {
      "tmux" = {
        source = "${configDir}/tmux";
        recursive = true;
      };
      "tmux/extraInit".text = ''
        # This file is auto-generated by nixos, don't edit by hand!
        
        # Load theme config FIRST (sets status-left placeholder and @prefix_highlight_* options)
        # This must happen before prefix-highlight plugin runs so it can replace the placeholder
        ${concatMapStrings (path: ''
          source '${path}'
        '') cfg.rcFiles}

        # Plugins (prefix-highlight can now replace #{prefix_highlight} placeholder)
        run-shell ${pkgs.tmuxPlugins.copycat}/share/tmux-plugins/copycat/copycat.tmux
        run-shell ${pkgs.tmuxPlugins.prefix-highlight}/share/tmux-plugins/prefix-highlight/prefix_highlight.tmux
        run-shell ${pkgs.tmuxPlugins.yank}/share/tmux-plugins/yank/yank.tmux
        run-shell ${tmux-opencode-status}/opencode-status.tmux
        
        # tmux-window-name: Smart automatic window naming based on path and running program
        # Abbreviations: OC=opencode, CC=claude, V=vim/nvim, G=git, JJ=jjui, λ=shell
        set -g @tmux_window_name_shells "['zsh', 'bash', 'sh', 'fish']"
        set -g @tmux_window_name_dir_programs "['nvim', 'vim', 'vi', 'git', 'jjui', 'opencode', 'claude']"
        set -g @tmux_window_name_use_tilde "True"
        set -g @tmux_window_name_max_name_len "24"
        set -g @tmux_window_name_substitute_sets "[('.*node.*opencode.*', 'opencode'), ('.*node.*claude.*', 'claude'), ('^(/usr)?/bin/(.+)', '\\\\g<2>')]"
        set -g @tmux_window_name_icon_style "icon"
        set -g @tmux_window_name_custom_icons "{'opencode': 'OC', 'claude': 'CC', 'nvim': 'V', 'vim': 'V', 'vi': 'V', 'git': 'G', 'jjui': 'JJ', 'zsh': 'λ', 'bash': 'λ', 'sh': 'λ', 'fish': 'λ'}"
        run-shell ${pkgs.my.tmux-window-name}/share/tmux-plugins/tmux-window-name/tmux_window_name.tmux
      '';
    };

    env = {
      PATH = [ "$TMUXIFIER/bin" ];
      TMUX_HOME = "$XDG_CONFIG_HOME/tmux";
      TMUXIFIER = "$XDG_DATA_HOME/tmuxifier";
      TMUXIFIER_LAYOUT_PATH = "$XDG_DATA_HOME/tmuxifier";
    };
  };
}
