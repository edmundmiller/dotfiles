{
  config,
  pkgs,
  lib,
  ...
}:
with lib;
with lib.my;
let
  cfg = config.modules.shell.tmux;
  inherit (config.dotfiles) configDir;

  # Fetch tmux-dotbar - minimal dot-separated status bar theme
  tmux-dotbar = pkgs.fetchFromGitHub {
    owner = "vaaleyard";
    repo = "tmux-dotbar";
    rev = "f62692501114582aa5311158271ec39402dfcbcd";
    sha256 = "0y9hjw5rxr3684dcg8s5qc7r70p1ai39bjbadmn7vlnn6680qjzy";
  };

  # Fetch tmux-dark-notify - auto dark/light mode switching on macOS
  tmux-dark-notify = pkgs.fetchFromGitHub {
    owner = "erikw";
    repo = "tmux-dark-notify";
    rev = "c3f9a42aab305d95260155e14d34cdfdd212d21e";
    sha256 = "1qcqmq2hp8cb80m91bi2ryfgr8r0wgss3h1a4nlxgkx4w37aw1x3";
  };

  # Despite tmux/tmux#142, tmux will support XDG in 3.2. Sadly, only 3.0 is
  # available on nixpkgs, and 3.1b on master (tmux/tmux@15d7e56), so I
  # implement it myself:
  # Export environment variables with fallback defaults for when they aren't
  # set (e.g., ghostty launching with --noprofile --norc). These are needed
  # by the tmux config file itself for sourcing extraInit, swap-pane scripts,
  # and the reload binding.
  tmux = pkgs.writeScriptBin "tmux" ''
    #!${pkgs.stdenv.shell}
    export TMUX_HOME="''${TMUX_HOME:-$HOME/.config/tmux}"
    export DOTFILES="''${DOTFILES:-$HOME/.config/dotfiles}"
    export DOTFILES_BIN="''${DOTFILES_BIN:-$DOTFILES/bin}"
    exec ${pkgs.tmux}/bin/tmux -f "$TMUX_HOME/config" "$@"
  '';
in
{
  options.modules.shell.tmux = with types; {
    enable = mkBoolOpt false;
    rcFiles = mkOpt (listOf (either str path)) [ "${configDir}/tmux/theme.conf" ];
  };

  config = mkIf cfg.enable {
    user.packages = [
      tmux
      pkgs.my.tmux-file-picker
      pkgs.my.tmux-smooth-scroll
      pkgs.gum # Interactive CLI for bd-capture popup
    ];

    # Agent Icons font for PUA codepoints (U+F5000–F5003) rendered by Ghostty
    fonts.packages = [ pkgs.my.agent-icons ];
    modules.desktop.term.ghostty.configInit = mkIf config.modules.desktop.term.ghostty.enable ''
      # Map agent icon PUA codepoints to the Agent Icons font
      font-codepoint-map = U+F5000-U+F5003=Agent Icons
    '';

    modules.theme.onReload.tmux = "${tmux}/bin/tmux source-file $TMUX_HOME/extraInit";

    modules.shell.zsh = {
      rcInit = "_cache tmuxifier init -";
      rcFiles = [ "${configDir}/tmux/aliases.zsh" ];
    };

    home.configFile = {
      "tmux" = {
        source = "${configDir}/tmux";
        recursive = true;
      };
      # Nix-generated theme files so dark-notify can re-run dotbar on switch
      "tmux/theme-dark-generated.conf".text = ''
        source '${configDir}/tmux/theme-dark.conf'
        run-shell ${tmux-dotbar}/dotbar.tmux
        run-shell ${pkgs.tmuxPlugins.prefix-highlight}/share/tmux-plugins/prefix-highlight/prefix_highlight.tmux
      '';
      "tmux/theme-light-generated.conf".text = ''
        source '${configDir}/tmux/theme-light.conf'
        run-shell ${tmux-dotbar}/dotbar.tmux
        run-shell ${pkgs.tmuxPlugins.prefix-highlight}/share/tmux-plugins/prefix-highlight/prefix_highlight.tmux
      '';
      "tmux/extraInit".text = ''
        # This file is auto-generated by nixos, don't edit by hand!

        # Load theme config FIRST (sets dotbar color options and pane/border styling)
        # Must happen before dotbar.tmux runs to pick up @tmux-dotbar-* options
        ${concatMapStrings (path: ''
          source '${path}'
        '') cfg.rcFiles}

        # Dotbar theme (must run before prefix-highlight)
        run-shell ${tmux-dotbar}/dotbar.tmux

        # Plugins (prefix-highlight can now replace #{prefix_highlight} placeholder)
        run-shell ${pkgs.tmuxPlugins.copycat}/share/tmux-plugins/copycat/copycat.tmux
        run-shell ${pkgs.tmuxPlugins.prefix-highlight}/share/tmux-plugins/prefix-highlight/prefix_highlight.tmux
        run-shell ${pkgs.tmuxPlugins.yank}/share/tmux-plugins/yank/yank.tmux

        # tmux-dark-notify: auto dark/light mode switching (macOS only)
        # Uses generated theme files that re-run dotbar on switch
        # NOTE: Can't use run-shell for main.tmux — its background daemon causes
        # run-shell to hang waiting for child processes. Start it detached instead.
        set -g @dark-notify-theme-path-light '$HOME/.config/tmux/theme-light-generated.conf'
        set -g @dark-notify-theme-path-dark '$HOME/.config/tmux/theme-dark-generated.conf'
        run-shell -b "${tmux-dark-notify}/main.tmux > /dev/null 2>&1"

        # tmux-smooth-scroll: Rust-based smooth scrolling (replaces azorng/tmux-smooth-scroll)
        # Run synchronously — init just calls bind-key N times, no reason to background it.
        # Backgrounding causes a race: old stale bindings fire before init finishes.
        run-shell '${pkgs.my.tmux-smooth-scroll}/bin/tmux-smooth-scroll init'

        # tmux-smart-name: window naming + AI agent status
        run-shell ${pkgs.my.tmux-smart-name}/share/tmux-smart-name/scripts/smart-name.sh
      '';
    };

    # tml — tmux dev layout: AI tool + lazygit + shell
    # Shell function in config/tml/aliases.zsh (auto-sourced by zsh module)
    home.configFile."lazygit/tml.yml".source = "${configDir}/lazygit/tml.yml";

    env = {
      PATH = [ "$TMUXIFIER/bin" ];
      TMUX_HOME = "$XDG_CONFIG_HOME/tmux";
      TMUXIFIER = "$XDG_DATA_HOME/tmuxifier";
      TMUXIFIER_LAYOUT_PATH = "$XDG_DATA_HOME/tmuxifier";
    };
  };
}
