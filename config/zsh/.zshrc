#!/usr/bin/env zsh
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Ensure essential environment variables are set
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export XDG_DATA_HOME="${XDG_DATA_HOME:-$HOME/.local/share}"
export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
export ZDOTDIR="${ZDOTDIR:-$XDG_CONFIG_HOME/zsh}"
export ZSH_CACHE="${ZSH_CACHE:-$XDG_CACHE_HOME/zsh}"

# Helper functions
function _source {
  [[ -f "$1" ]] && source "$1"
}

function _cache {
  local cache_dir="$XDG_CACHE_HOME/zsh"
  local cache_file="$cache_dir/$1.zsh"

  if [[ ! -f "$cache_file" ]] || [[ "$commands[$1]" -nt "$cache_file" ]]; then
    mkdir -p "$cache_dir"
    "$@" > "$cache_file"
  fi
  source "$cache_file"
}

# Source configuration
source $ZDOTDIR/config.zsh

# Initialize antidote (managed by Nix)
# Cache antidote path for faster startup
_antidote_cache_file="$ZSH_CACHE/antidote_path"
if [[ -f "$_antidote_cache_file" ]]; then
  _antidote_path="$(cat "$_antidote_cache_file")"
else
  # Find and cache antidote path
  for antidote_file in /nix/store/*antidote*/share/antidote/antidote.zsh; do
    if [[ -f "$antidote_file" ]]; then
      _antidote_path="$antidote_file"
      mkdir -p "$(dirname "$_antidote_cache_file")"
      echo "$_antidote_path" > "$_antidote_cache_file"
      break
    fi
  done
fi

# Source antidote if path exists
[[ -f "$_antidote_path" ]] && source "$_antidote_path"

# Load plugins from .zsh_plugins.txt
# Static file goes in cache since ZDOTDIR may be read-only (Nix symlinks)
_antidote_bundlefile="$ZDOTDIR/.zsh_plugins.txt"
_antidote_staticfile="$ZSH_CACHE/.zsh_plugins.zsh"

# Handle Nix store files: mtime is epoch 1, so antidote's freshness check fails.
# Instead, track the symlink target and regenerate when it changes.
if [[ -L "$_antidote_bundlefile" ]]; then
  _antidote_target=$(readlink "$_antidote_bundlefile")
  _antidote_marker="$ZSH_CACHE/.zsh_plugins.target"
  if [[ ! -f "$_antidote_marker" ]] || [[ "$(cat "$_antidote_marker" 2>/dev/null)" != "$_antidote_target" ]]; then
    # Symlink target changed - force regeneration
    rm -f "$_antidote_staticfile" "$_antidote_staticfile.zwc"
    mkdir -p "$ZSH_CACHE"
    echo "$_antidote_target" > "$_antidote_marker"
  fi
fi

antidote load "$_antidote_bundlefile" "$_antidote_staticfile"

# Compile static file for faster loading (background, non-blocking)
if [[ ! -f "$_antidote_staticfile.zwc" ]] || [[ "$_antidote_staticfile" -nt "$_antidote_staticfile.zwc" ]]; then
  zcompile "$_antidote_staticfile" &!
fi

unset _antidote_bundlefile _antidote_staticfile _antidote_target _antidote_marker

## Bootstrap interactive sessions
if [[ $TERM != dumb ]]; then
  # Note: compinit is called in completion.zsh after fpath setup
  # (we use manual init instead of nix-darwin's enableGlobalCompInit)

  # Add custom completions directory before sourcing other configs
  fpath=($ZDOTDIR/completions $fpath)

  # JJ completion setup - choose one method:
  # Option 1: Fast static completion (current, ~6ms)
  # (already loaded via fpath/_jj_fast)

  # Option 2: Official dynamic completion (~9ms, more features)
  # Uncomment to use dynamic completion instead of static:
  # eval "$(COMPLETE=zsh jj)" 2>/dev/null

  source $ZDOTDIR/keybinds.zsh
  source $ZDOTDIR/completion.zsh
  source $ZDOTDIR/aliases.zsh

  alias jj="jj --config width=$(tput cols)"

  # Source Claude-specific aliases if claude module is enabled
  _source ${XDG_CONFIG_HOME:-$HOME/.config}/dotfiles/config/claude/aliases.zsh

  # Auto-generated by nixos
  _source $ZDOTDIR/extra.zshrc
  # If you have host-local configuration, put it here
  _source $ZDOTDIR/local.zshrc

  # P10k config already loaded above when P10k theme was loaded
  # [[ -f $ZDOTDIR/.p10k.zsh ]] && source $ZDOTDIR/.p10k.zsh

  # Initialize zoxide with caching (needed for aliases)
  if (( $+commands[zoxide] )); then
    _cache zoxide init zsh || true
  fi

  # Defer only autopair which is not immediately needed
  {
    # Initialize autopair (deferred - happens when plugins are fully loaded)
    if (( $+functions[autopair-init] )); then
      autopair-init
    fi
  } &!

  # Defer external tool initialization to avoid p10k instant prompt I/O warning
  {
    # fnm (Fast Node Manager)
    if (( $+commands[fnm] )); then
      eval "$(fnm env --use-on-cd --shell zsh)"
    fi

    # bun completions
    [ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"

    # sdkman (must be last per sdkman docs)
    [[ -s "$HOME/.sdkman/bin/sdkman-init.sh" ]] && source "$HOME/.sdkman/bin/sdkman-init.sh"
  } &!
fi

export PATH="$HOME/.pixi/bin:$PATH"
export PATH="$HOME/.local/bin:$PATH"

# bun
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"

# todo.txt configuration (managed in dotfiles, symlinked to ~/.todo.actions.d)
# export TODOTXT_CFG_FILE="$HOME/.config/dotfiles/config/todotxt/todo.cfg"
# export TODO_CFG_FILE="$HOME/.config/dotfiles/config/todotxt/todo.cfg"  # compatibility with some variants
# if [ -r "$TODOTXT_CFG_FILE" ]; then
#   source "$TODOTXT_CFG_FILE"
# fi

# alias t="todo.sh"
# alias ta="t add"
# alias td="t do"
# alias ttoday="t today"

export NXF_SYNTAX_PARSER=v2

alias vanguard="$(go env GOPATH)/bin/vanguard"

# Amp CLI
export PATH="/Users/emiller/.amp/bin:$PATH"

# Goose terminal integration
if (( $+commands[goose] )); then
  eval "$(goose term init zsh)"
fi
