#!/usr/bin/env zsh
# Obsidian CLI
export PATH="$PATH:/Applications/Obsidian.app/Contents/MacOS"

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Ensure essential environment variables are set
export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export XDG_DATA_HOME="${XDG_DATA_HOME:-$HOME/.local/share}"
export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
export ZDOTDIR="${ZDOTDIR:-$XDG_CONFIG_HOME/zsh}"
export ZSH_CACHE="${ZSH_CACHE:-$XDG_CACHE_HOME/zsh}"

# Helper functions
function _source {
  [[ -f "$1" ]] && source "$1"
}

function _cache {
  local cache_dir="$XDG_CACHE_HOME/zsh"
  local cache_file="$cache_dir/$1.zsh"
  
  if [[ ! -f "$cache_file" ]] || [[ "$commands[$1]" -nt "$cache_file" ]]; then
    mkdir -p "$cache_dir"
    "$@" > "$cache_file"
  fi
  source "$cache_file"
}

# Source configuration
source $ZDOTDIR/config.zsh

# Load plugins via antidote's static file
# If the static file exists, source it directly (fast path ~27ms vs ~41ms)
# Run `antidote bundle < $ZDOTDIR/.zsh_plugins.txt > $ZSH_CACHE/.zsh_plugins.zsh`
# to regenerate after changing .zsh_plugins.txt
ANTIDOTE_STATIC_FILE="$ZSH_CACHE/.zsh_plugins.zsh"
if [[ -f "$ANTIDOTE_STATIC_FILE" ]]; then
  source "$ANTIDOTE_STATIC_FILE"
else
  # Cold start: find antidote and generate static file
  _antidote_cache_file="$ZSH_CACHE/antidote_path"
  if [[ -f "$_antidote_cache_file" ]]; then
    _antidote_path="$(cat "$_antidote_cache_file")"
  else
    for antidote_file in /nix/store/*antidote*/share/antidote/antidote.zsh; do
      if [[ -f "$antidote_file" ]]; then
        _antidote_path="$antidote_file"
        mkdir -p "${_antidote_cache_file:h}"
        echo "$_antidote_path" > "$_antidote_cache_file"
        break
      fi
    done
  fi
  [[ -f "$_antidote_path" ]] && source "$_antidote_path"
  antidote load "$ZDOTDIR/.zsh_plugins.txt"
fi

# P10k is loaded by antidote from .zsh_plugins.txt
# Load p10k config after antidote has sourced the theme
[[ -f $ZDOTDIR/.p10k.zsh ]] && source $ZDOTDIR/.p10k.zsh

## Bootstrap interactive sessions
if [[ $TERM != dumb ]]; then
  # nix-darwin handles compinit via enableGlobalCompInit = true
  # so we don't need to manually initialize it here
  
  # Add custom completions directory before sourcing other configs
  fpath=($ZDOTDIR/completions $fpath)

  # JJ completion setup - choose one method:
  # Option 1: Fast static completion (current, ~6ms)
  # (already loaded via fpath/_jj_fast)

  # Option 2: Official dynamic completion (~9ms, more features)
  # Uncomment to use dynamic completion instead of static:
  # eval "$(COMPLETE=zsh jj)" 2>/dev/null

  source $ZDOTDIR/keybinds.zsh
  source $ZDOTDIR/completion.zsh

  # Auto-generated by nixos
  _source $ZDOTDIR/extra.zshrc
  # If you have host-local configuration, put it here
  _source $ZDOTDIR/local.zshrc

  # P10k config already loaded above when P10k theme was loaded
  # [[ -f $ZDOTDIR/.p10k.zsh ]] && source $ZDOTDIR/.p10k.zsh

  # Initialize zoxide with caching (needed for aliases)
  if (( $+commands[zoxide] )); then
    _cache zoxide init zsh
  fi
  
  # Defer only autopair which is not immediately needed
  {
    # Initialize autopair (deferred - happens when plugins are fully loaded)
    if (( $+functions[autopair-init] )); then
      autopair-init
    fi
  } &!
fi

export PATH="/Users/emiller/.pixi/bin:$PATH"
export PATH="$HOME/.local/bin:$PATH"

# bun - lazy load completions
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"
if [[ -s "$BUN_INSTALL/_bun" ]]; then
  function bun {
    unfunction bun bunx
    source "$BUN_INSTALL/_bun"
    command bun "$@"
  }
  function bunx { bun; command bunx "$@"; }
fi

alias t="todo.sh"
alias ta="t add"
alias td="t do"
alias ttoday="t today"

# SDKMAN - lazy load (~100ms savings)
export SDKMAN_DIR="$HOME/.sdkman"
if [[ -s "$SDKMAN_DIR/bin/sdkman-init.sh" ]]; then
  function sdk {
    unfunction sdk java gradle kotlin groovy maven
    source "$SDKMAN_DIR/bin/sdkman-init.sh"
    sdk "$@"
  }
  for cmd in java gradle kotlin groovy maven; do
    eval "function $cmd { sdk; command $cmd \"\$@\"; }"
  done
fi

# Entire CLI shell completion (use _cache to avoid slow subshell on every start)
if (( $+commands[entire] )); then
  _cache entire completion zsh
fi
